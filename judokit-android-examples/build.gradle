apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

apply from: '../ktlint.gradle'
apply plugin: 'org.jetbrains.kotlin.android'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def keystorePropertiesFile = rootProject.file("judokit-android-examples/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace 'com.judokit.android.examples'
    compileSdk 33

    defaultConfig {
        applicationId "com.judokit.android.examples"
        minSdk 21
        targetSdk 33
        versionCode 4109
        versionName "4.1.5 (1) -" + getGitHash()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
        buildConfigField "String", "JUDO_ID", "\"" + System.getenv("JUDO_ID") + "\""
        buildConfigField "String", "API_TEST_TOKEN", "\"" + System.getenv("API_TEST_TOKEN") + "\""
        buildConfigField "String", "API_TEST_SECRET", "\"" + System.getenv("API_TEST_SECRET") + "\""
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
        }
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
        }
    }
    testOptions {
        animationsDisabled = true
    }
    buildFeatures {
        viewBinding = true
        compose true
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":judokit-android")

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation "com.github.chuckerteam.chucker:library:3.5.2"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation platform('androidx.compose:compose-bom:2023.01.00')
    implementation 'androidx.compose.material:material'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.test.ext:junit-ktx:1.1.5'
    implementation 'androidx.test.espresso:espresso-contrib:3.5.1'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    api "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    ktlint 'com.pinterest:ktlint:0.47.1'

    implementation platform('com.google.firebase:firebase-bom:31.1.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    androidTestImplementation "androidx.test:core-ktx:$androidx_test_version"
    androidTestImplementation 'com.google.code.gson:gson:2.9.1'

    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test:runner:$androidx_test_runner_version"
    androidTestImplementation "androidx.test:rules:$androidx_test_version"
    androidTestImplementation "androidx.test.espresso:espresso-idling-resource:$espresso_version"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.3.3"
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.3.3"
    androidTestImplementation "androidx.test.espresso:espresso-web:3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"
    androidTestImplementation "androidx.room:room-runtime:$room_version"
}
