apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'guru.stefma.bintrayrelease'
apply from: '../code_analysis/code_analysis.gradle'

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName '8.1.3'
        vectorDrawables.useSupportLibrary = true
        consumerProguardFiles 'proguard-rules.pro'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
    }
    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
    }

    lintOptions {
        abortOnError false
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

version = android.defaultConfig.versionName
group = 'com.judopay'
androidArtifact {
    artifactId = 'android-sdk'
}
publish {
    userOrg = 'judopay'
    desc = 'Android Mobile Payments SDK'
    website = 'https://github.com/JudoPay/Judo-Android'
}

task(cleanMavenArtifact) {
    doLast {
        def binDir = new File("${System.getProperty('user.home')}/.m2/repository/com/judopay/${androidArtifact.artifactId}/${version}")
        binDir.deleteDir()
    }
}

task androidJavadocs(type: Javadoc) {
    title "Judo Android SDK Documentation"
    options {
        links "https://docs.oracle.com/javase/7/docs/api/"
        links "https://developer.android.com/reference/"
        linksOffline("https://developer.android.com/reference", "${android.sdkDirectory}/docs/reference")
        encoding "utf-8"
        addStringOption('Xdoclint:none', '-quiet')
    }
    source = android.sourceSets.main.java.srcDirs
    excludes = ['**/BuildConfig.java', '**/R.java', '**/*.kt']
    afterEvaluate {
        classpath += files(android.getBootClasspath())
        classpath += files(android.libraryVariants.collect { it.javaCompile.classpath.files })
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadocJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

dependencies {
    implementation 'com.judopay:device-dna:1.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.12.1'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core:1.2.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'androidx.annotation:annotation:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.2.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    implementation 'com.readystatesoftware.chuck:library:1.1.0'

    implementation('com.google.android.gms:play-services-wallet:18.0.0') {
        exclude group: 'com.android.support', module: 'support-media-compat'
        exclude group: 'com.android.support', module: 'support-v4'
    }

    // Room
    implementation "androidx.room:room-runtime:2.2.4"
    kapt "androidx.room:room-compiler:2.2.4"
    // Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:2.2.4"


    implementation "androidx.core:core-ktx:1.2.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.61"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "cards.pay:paycardsrecognizer:1.1.0"

    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:3.2.0'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'

    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:core:1.2.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.2.0'
}
