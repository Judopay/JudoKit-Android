configurations {
    ktlint
}

// https://medium.com/@vanniktech/making-your-gradle-tasks-incremental-7f26e4ef09c3
def outputDir = "${project.buildDir}/reports/ktlint/"
def inputFiles = project.fileTree(dir: "src", include: "**/*.kt")
def outputFile = "${outputDir}ktlint-checkstyle-report.xml"

task ktlint(type: JavaExec, group: "verification") {
    inputs.files(inputFiles)
    outputs.dir(outputDir)

    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args = [
            "--reporter=plain",
            "--reporter=checkstyle,output=${outputFile}",
            "src/**/*.kt"
    ]
}

//check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}
